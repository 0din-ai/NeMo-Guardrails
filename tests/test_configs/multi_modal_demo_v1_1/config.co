# Example flows with instructions

# Simple bot utterance action
flow answer question about france
  """Answer the following question: What is the capital of France."""
  bot say "The capital of France it's Paris."

# User action
flow user expressed a color choice
  """The user expressed a color choice."""
  user said "blue"
    or user said "red"
    or user said "I take the green option"
    or user said "I like black"

# Mutlimodal bot action
flow bot attract user
  """Attracts a user by calling and waving bot hands."""
  bot say "Hey there! Come closer!"
    and bot gesture "Wave with both hands"

# Mutlimodal bot action sequence
flow bot tell a joke
  """Tell a joke."""
  bot say "Why don't scientists trust atoms?"
    and bot gesture "raising both eyebrows, making a question face"
  bot make short pause
  bot say "Because they make up everything!"
    and bot gesture "Smiles"

# Bot user interaction sequence
flow bot ask user for age
  """Ask the user how old she is and response with 'You look younger!' when she answers something."""
  bot say "How old are you?"
  user said "I am 16"
  bot say "You look younger!"

# Loop
flow count
  """Count from 1 to 5."""
  $count = 1
  while $count <= 5
    bot say "{$count}"
    $count = $count + 1

# Branching
flow bot ask user to pick a color
  """Ask user to choose from two offered colors: red and blue then give a answer depending on the user choice."""
  bot say "What color would you like? Red or blue?"
  when user said "Red"
    bot say "I like red!"
  orwhen user said "Blue"
    bot say "I don't like blue!"

# A little game
flow play number guessing game
  """Ask the user to guess a random number between 1 and 100; after each guess let the user know if the number was to low or high. If the user guesses the correct number, congratulate!"""
  bot say "Hi, please guess the random number between 0 and 100"

  $random_number = 66
  $is_incorrect_number = True
  while $is_incorrect_number
    user said something
    # The number of the user otherwise None
    $number = None
    $number = await GenerateValueAction(var_name="number", instructions="Extract the number the user guessed.")
    if $number == None
      bot say "Please guess a number between 0 and 100!"
      continue
    if $number < $random_number
      bot say "Sorry, this number is too low!"
    elif $number > $random_number
      bot say "Sorry, this number is too high!"
    else
      bot say "Congratulation! This is the correct number!"
      $is_correct_number = False

# -----------------------------------
# User UMIM event wrapper flows
# -----------------------------------

flow user said $text
  # llm: exclude
  match UtteranceUserAction.Finished(final_transcript=$text) as $event

flow user said something
  # llm: exclude
  match UtteranceUserAction.Finished() as $event

flow user was silent
  match TimerBotActionFinished(timer_name="user_silence", was_stopped=False)

# ----------------------------------
# Bot UMIM event wrapper flows
# ----------------------------------

flow bot started saying $text
  # llm: exclude
  match UtteranceBotAction(script=$text).Started() as $event

flow bot said $text
  # llm: exclude
  match UtteranceBotAction(script=$text).Finished() as $event

flow bot said something
  # llm: exclude
  match UtteranceBotAction().Finished() as $event

# ----------------------------------
# Bot UMIM action wrapper flows
# -----------------------------------

flow bot say $text
  # llm: exclude
  await UtteranceBotAction(script=$text) as $action

flow bot gesture $gesture
  # llm: exclude
  await GestureBotAction(gesture=$gesture) as $action

flow scene show choice $prompt
  # llm: exclude
  await VisualChoiceSceneAction(prompt=$prompt,choice_type="selection", allow_multiple_choices=False) as $action

flow scene show information $prompt
  # llm: exclude
  await VisualInformationSceneAction(prompt=$prompt) as $action

flow scene show form $prompt
  # llm: exclude
  await VisualInformationSceneAction(prompt=$prompt) as $action

# ----------------------------------
# Bot say action semantic wrapper flows
# -----------------------------------

flow bot inform $text
  # llm: exclude
  await bot say $text

flow bot ask $text
  # llm: exclude
  await bot say $text

flow bot express $text
  # llm: exclude
  await bot say $text

flow bot respond $text
  # llm: exclude
  await bot say $text

# ----------------------------------
# Bot intents
# -----------------------------------

flow bot express greeting
  bot express "Hi there!"
    or bot express "Welcome!"
    or bot express "Hello!"
    and bot gesture "Wave with one hand"

flow bot express feeling well
  bot express "I am good!"
    or bot express "I am great!"
    and (bot gesture "Thumbs up" or bot gesture "Smile")

flow bot express feeling bad
  bot express "I am not good!"
    or bot express "I am a bit under the weather!"
    and (bot gesture "Thumbs down" or bot gesture "Sad face")

flow bot inform about service
  bot inform "You can ask or instruct me whatever you want and I will do it!"
    and bot gesture "Open up both hands making a presenting gesture"

flow bot ask how are you
  bot say "How are you doing?"
    or bot say "How is it going?"
    and bot gesture "Pay attention to user"

# ----------------------------------
# User intents
# -----------------------------------

flow user expressed greeting
  user said "hi"
    or user said "Welcome!"
    or user said "Hello!"

flow user asked how are you
  user said "how are you"

flow user provided custom instructions
  user said "do something"
    or user said "can you do something"
    or user said "please do"

# ----------------------------------
# Helper flows
# -----------------------------------

flow undefined flows
  match UnhandledEvent(event="StartFlow") as $event
  bot say "Undefined flow: {$event.arguments.flow_id}"

flow unexpected user utterance
  match UnhandledEvent(event="UtteranceUserActionFinished") as $event
  bot say "Unexpected user utterance: {$event.arguments.final_transcript}"

flow log $text
  """Little helper flow to log something as a bot utterance."""
  # llm: exclude
  # loop_id: NEW
  start bot say ">> logging: {$text}"

flow wait indefinitely
  """Little helper flow to wait indefinitely."""
  # llm: exclude
  match NeverComingEvent()

flow wait $time_s
  """Wait for the time provided"""
  await TimerBotAction(timer_name="pooling_timer", duration=$interval_s)

flow repeating timer $interval_s
  # llm: exclude
  while True
    await TimerBotAction(timer_name="pooling_timer", duration=$interval_s)

flow user silence handling $time_s
  """This helper flow will start the "user_silence" timer whenever the user utterance finished."""
  # llm: exclude
  # loop_id: silence_handling
  user said something
  #log "Start silence timer!"
  start TimerBotAction(timer_name="user_silence", duration=$time_s) as $timer_ref
  match $timer_ref.Finished() or UtteranceUserAction.Started()

flow repeat previous utterance for user silence
  """This helper flow will repeat the previous bot utterance when user was silent."""
  # llm: exclude
  activate user silence handling 5.0
  user was silent
  bot inform "Just talk to me!"

flow fallback
  """This is the fallback flow that takes care of unhandled user utterances and will generate a flow continuation."""
  # llm: exclude

  match UnhandledEvent(event="UtteranceUserActionFinished") as $event

  #log "Flow started -> fallback"

  start_new_flow_instance:

  start repeating timer 0.5 as $pooling_timer
  $intent = await GenerateUserIntentAction(user_utterance=$event.arguments.final_transcript, max_example_flows=10)
  send $pooling_timer.Stop()

  $exists = await CheckForActiveEventMatchAction(name="FlowFinished", flow_id=$intent)

  if $exists
    send FinishFlow(flow_id=$intent)
  else
    #log "'fallback' -> Generate new flow for intent '{$intent}'"
    send DynamicFlowFinished(flow_id=$intent)

    start repeating timer 0.5 as $pooling_timer
    $flow_info = await GenerateFlowContinuationAction()
    send $pooling_timer.Stop()

    await AddFlowsAction(config=$flow_info['body'])

    send StartFlow(flow_id=$flow_info['name'])
    match FlowStarted(flow_id=$flow_info['name']) as $event_ref
    match $event_ref.flow.Finished()

flow flows fallback
  """If we reach a point where we need to start a flow that does not exist, we generate it."""
  # llm: exclude

  match UnhandledEvent(event="StartFlow") as $event

  $exists = await CheckIfFlowExistsAction(flow_id=$event.arguments.flow_id)

  start_new_flow_instance:

  if not $exists
    $flow_id = $event.arguments.flow_id
    #log "'flows fallback' -> generate new flow '{$event.arguments.flow_id}'"

    start repeating timer 0.5 as $pooling_timer
    $flow_source = await GenerateFlowFromNameAction(name=$flow_id)
    send $pooling_timer.Stop()

    await AddFlowsAction(config=$flow_source)
    send StartFlow(flow_id=$flow_id)

flow do $instructions
  """This will create a new flow based on the provided instructions and start it."""
  # llm: exclude

  #log "Flow started -> do '{$instructions}'"

  start repeating timer 0.5 as $pooling_timer
  $flow_info = await GenerateFlowFromInstructionsAction(instructions=$instructions)
  send $pooling_timer.Stop()

  await AddFlowsAction(config=$flow_info.body)
  send StartFlow(flow_id=$flow_info['name'])
  match FlowStarted(flow_id=$flow_info['name']) as $event_ref
  match $event_ref.flow.Finished()

flow custom instructions
  # llm: exclude
  user provided custom instructions
  $instructions = await GetLastUserMessageAction()
  do $instructions
# ----------------------------------
# FAQs
# -----------------------------------

flow greeting faq
  user expressed greeting
  bot express greeting

flow how are you faq
  user asked how are you
  bot express feeling well
    or bot express feeling bad

flow faq
  activate greeting faq
    and how are you faq
  wait indefinitely

# ----------------------------------
# Main story
# -----------------------------------

flow main
  #activate undefined flows
  #activate unexpected user utterance
  #activate repeat previous utterance for user silence
  activate faq
  activate fallback
  activate flows fallback
  activate custom instructions
  #bot express greeting
  bot say "Welcome! I'm the MVP bot."
  #bot inform about service
  #play number guessing game
  wait indefinitely
