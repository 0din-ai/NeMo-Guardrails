flow showcase backchannelling interaction
  bot inform "First, I am going to ask you about your day and will react to keywords such as nice, great or terrible."
  bot inform "Try to answer with a long sentence that contains one of these keywords to see the effect."
  wait 1.0
  bot asks user how the day was

  bot inform "Now I am going to demonstrate how I can dynamically react to other types of inputs. As an example we use a form."
  wait 1.0
  bot asks email address

flow user asked bot to initiate back channelling interaction
  user said "I want to see the back channeling example"
    or user said "can you do the back channeling"

flow user saying something bad
  user saying "terrible"
    or user saying "horrible"
    or user saying "bad"

flow user saying something nice
  user saying "nice"
    or user saying "great"
    or user saying "perfect"
    or user saying "awesome"

flow user said something bad
  user said r".*terribl|horribl|bad.*"

flow bot reacts to bad things
  user saying something bad
  bot say "Ohh"
    and bot gesture "shake head"

flow bot reacts to nice things
  user saying something nice
  bot say "Nice"
    and bot gesture "celebrate something going well"

flow bot asks user how the day was
  activate bot reacts to nice things as $react_nice_flow
  activate bot reacts to bad things as $react_bad_flow

  bot say "How was your day?"
  start bot posture "attentive"

  when user said something bad
    bot say "I hope you are alright now?"
      and bot gesture "looking concerned to the user"
  orwhen user said something
    bot say "Great. And any plans for the rest of the day?"
  send $react_nice_flow.Stop()
  send $react_bad_flow.Stop()

  user said something
  bot say "Thanks for sharing"


flow bot asks email address
  bot ask "Please enter your email address to continue"
  start VisualFormSceneAction(prompt="Enter your email",inputs=[{"id": "email", "description": "email address", "value" : ""}]) as $action

  while True
    when VisualFormSceneAction.InputUpdated(interim_inputs=[{"id": "email",  "value" : r"@$"}])
        bot say "And now only the domain missing!"
    orwhen VisualFormSceneAction.InputUpdated(interim_inputs=[{"id": "email",  "value" : r"\.org|\.com$"}])
        bot say "Looks like a valid email address to me" and bot gesture "success"
    orwhen VisualFormSceneAction.ConfirmationUpdated()
        bot say "Thank you" and bot gesture "bowing"
        break