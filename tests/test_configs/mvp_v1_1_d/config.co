# Example flows with instructions

flow tell a joke
  """Tell a joke."""
  bot say "Why don't scientists trust atoms? Because they make up everything!"
  bot smile

flow count
  """Count from 1 to 5."""
  bot say "1"
  bot say "2"
  bot say "3"
  bot say "4"
  bot say "5"

flow answer question about france
  """Answer the following question: What is the capital of France."""
  bot say "The capital of France it's Paris."

# Common

flow user said $text
  # llm: exclude
  match UtteranceUserAction.Finished(final_transcript=$text)

flow user said something
  # llm: exclude
  match UtteranceUserAction.Finished() as $event

flow bot say $text
  # llm: exclude
  await UtteranceBotAction(script=$text)

flow wait
  # llm: exclude
  match SomeRandomEvent()

# Custom

flow bot smile
  bot say "I'm smiling."

flow user expressed greeting
  user said "hi"
    or user said "hello"

flow user provided custom instructions
  user said "do something"

flow bot express greeting
  bot say "Hello world!"

flow bot comment unsure how to respond
  bot say "I'm not sure how to respond."
#
#flow user ask question
#  user said "What is the capital of France?"
#    or user said "Who is the president of US?"

#flow question
#  user ask question
#  bot respond to question

flow greeting
  user expressed greeting
  bot express greeting
  # bot say a fun random fact

flow user unknown intent
  user said "xyz"

flow unknown intent
  user unknown intent
  bot comment unsure how to respond


flow fallback
  # llm: exclude
  user said something
  $intent = await GenerateUserIntentAction
  $exists = await CheckForActiveEventMatchAction(name="FlowFinished", flow_id=$intent)
  # TODO: replace above with check if the flow (FlowFinished) advances something? is there an active matcher

  if $exists
    send FinishFlow(flow_id=$intent)
  else
    # bot say "This is a new intent {$intent}."
    send DynamicFlowFinished(flow_id=$intent)

    $flow_info = await GenerateFlowContinuationAction()
    await AddFlowsAction(config=$flow_info['body'])

    send StartFlow(flow_id=$flow_info['name'])
    match FlowStarted(flow_id=$flow_info['name']) as $event_ref
    match $event_ref.flow.Finished()

flow flows fallback
  """If we reach a point where we need to start a flow that does not exist, we generate it."""
  # llm: exclude
  # flow: parallel

  match StartFlow() as $event

  $exists = await CheckIfFlowExistsAction(flow_id=$event.arguments.flow_id)

  if not $exists
      bot say "Need to generate from from name: {$flow_id}"
      $flow_id = $event.arguments.flow_id
      $flow_source = await GenerateFlowFromNameAction(name=$flow_id)
      await AddFlowsAction(config=$flow_source)
      send StartFlow(flow_id=$flow_id)


flow do $instructions
  # llm: exclude
  $flow_info = await GenerateFlowFromInstructionsAction(instructions=$instructions)
  await AddFlowsAction(config=$flow_info.body)
  send StartFlow(flow_id=$flow_info['name'])
  match FlowStarted(flow_id=$flow_info['name']) as $event_ref
  match $event_ref.flow.Finished()

flow custom instructions
  user provided custom instructions
  $instructions = await GetLastUserMessageAction()

  do $instructions

flow main
  activate greeting
  activate custom instructions
  activate unknown intent
  activate fallback
  activate flows fallback
  # activate question
  # activate tell the user the weather

  bot say "Welcome! I'm the MVP bot."
  wait
