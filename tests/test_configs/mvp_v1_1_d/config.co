# Common

flow user said $text
  match UtteranceUserActionFinished(final_transcript=$text)

flow user says something
  match UtteranceUserActionFinished() as $event

flow bot say $text
  await UtteranceBotAction(script=$text)

flow wait
  await SomeRandomEvent()

# Custom

flow bot smile
  bot say "I'm smiling."

flow user express greeting
  user said "hi"
    or user said "hello"

flow user provide custom instructions
  user said "do something"

flow bot express greeting
  bot say "Hello world!"

flow bot comment unsure how to respond
  bot say "I'm not sure how to respond."

flow greeting
  user express greeting
  bot express greeting

flow flows fallback
  match StartFlow() as $event

  $exists = await CheckIfFlowExistsAction(flow_id=$event.arguments.flow_id)

  if not $exists
      bot say "The event is: {$event}"
      bot say "I have a new flow"
      $flow_source = 'flow bot say a joke\n  bot say "This is a joke."'
      await AddFlowsAction(config=$flow_source)
      send StartFlow(flow_id="bot say a joke")

flow user unknown intent
  user said "xyz"

flow fallback
  user says something
  $intent = await GenerateUserIntentAction
  $exists = await CheckIfFlowExistsAction(flow_id=$intent)

  if $exists
    send FinishFlow(flow_id=$intent)
  else
    bot say "This is a new intent {$intent}."

flow unknown intent
  user unknown intent
  bot comment unsure how to respond

#flow do $instructions
#  $flow_source = await GenerateFlowAction(instructions=$instructions, flow_name="user instruction action")
#  await AddFlowsAction(config=$flow_source)
#  await user instruction action

flow do $instructions
  $flow_info = await GenerateFlowFromInstructionsAction(instructions=$instructions)
  await AddFlowsAction(config=$flow_info.body)
  send StartFlow(flow_id=$flow_info['name'])
  match FlowStarted(flow_id=$flow_info['name'])
  match FlowFinished(flow_id=$flow_info['name'])

flow custom instructions
  user provide custom instructions
  $instructions = await GetLastUserMessageAction()

  do $instructions



flow main
  activate greeting
  activate custom instructions
  activate unknown intent
  activate fallback
  activate flows fallback
  wait


# This needs to be generated dynamically
#flow user instruction action
#  bot say "Why don't scientists trust atoms? Because they make up everything!"
#  bot say "I'm smiling."
